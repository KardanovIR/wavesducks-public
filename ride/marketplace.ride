{-# STDLIB_VERSION 5 #-}
{-# SCRIPT_TYPE ACCOUNT #-}
{-# CONTENT_TYPE DAPP #-}

let incubatorAddress = base58'3PEktVux2RhchSN63DsDo4b4mz4QqzKSeDv'
let breederAddress = base58'3PDVuU45H7Eh5dmtNbnRNRStGwULA7NY6Hb'
let artefactsAddress = base58'3P5E9xamcWoymiqLx8ZdmR7o4fJSRMGp1WR'
let feeAggregator = base58'3P6AobNbcQ3ZjcRY37mJPKN8PP9Giz8kgeY'
let eggAssetId = base58'C1iWsKGqLwjHUndiQ7iXpdmPum9PeCDFfyXBdJJosDRS'
let kGlobalCounter = "GLOBAL_COUNTER"

let feePercent = 5
let artefactsFeePercent = 20

func tryGetInteger(key: String) = {
    let val = match getInteger(this, key) {
        case b:Int => b
        case _ => 0
    }
    val
}

func tryGetString(key: String) = {
    let val = match getString(this, key) {
        case b:String => b
        case _ => ""
    }
    val
}

func tryGetBoolean(key: String) = {
    match getBoolean(this, key) {
        case b:Boolean => b
        case _ => false
    }
}

func getGlobalCounter() = {
  tryGetInteger(kGlobalCounter)
}

func getAssetGenotype(assetId: ByteVector) = {
  if (assetInfo(assetId).value().issuer == Address(artefactsAddress)) then "ART" else {
  let asset1 = match getString(Address(incubatorAddress), assetId.toBase58String()) {
        case b:String => b
        case _ => ""
    }
  if (asset1 != "") then {
    asset1
  } else {
    let asset2 = match getString(Address(breederAddress), assetId.toBase58String()) {
      case c:String => c
      case _ => ""
    }
    if (asset2 != "") then {
      asset2
    } else {
      ""
    }
  }
  }
}

func getBidCancelResults(auctionId: String, bidId: String) = {
  let isEggSell = tryGetBoolean("auction_" + auctionId + "_eggSell")
  let paymentAssetId = if isEggSell == true then eggAssetId else unit

  let assetId = this.getStringValue("auction_" + auctionId + "_assetId")

  let bidAuthor = tryGetString("auction_" + auctionId + "_bid_" + bidId + "_author")
  let bidAmount = tryGetInteger("auction_" + auctionId + "_bid_" + bidId + "_amount")
  [
    StringEntry("assetId_" + assetId + "_bid_" + bidId + "_data", "{\"auctionId\": \"" + auctionId + "\", \"author\": \"" + bidAuthor + "\", \"amount\": \"" + bidAmount.toString() + "\", \"status\": \"cancelled\"}"),
    StringEntry("auction_" + auctionId + "_bid_" + bidId + "_status", "cancelled"),
    DeleteEntry("address_" + bidAuthor + "_auction_" + auctionId + "_bid_" + bidId),
    ScriptTransfer(Address(bidAuthor.fromBase58String()), bidAmount, paymentAssetId)
  ]
}

@Callable(i)
func initAuction(startPrice: Int, instantPrice: Int, description: String, eggSell: String) = {
  let globalCounter = getGlobalCounter()
  let auctionId = i.transactionId.toBase58String() + globalCounter.toString()
  let pmt = i.payments[0].value()
  let assetId = pmt.assetId.value()

  let asset = getAssetGenotype(assetId)

  if (pmt.amount != 1) then throw("NFT is not attached") else
  if (asset == "") then {
    throw("You can only sell an NFT-duck created in incubator or breeder")
  } else {
    [
      StringEntry("auction_" + auctionId + "_status", "open"),
      BooleanEntry("auction_" + auctionId + "_eggSell", eggSell == "true"),
      IntegerEntry(kGlobalCounter, globalCounter + 1),
      StringEntry("auction_" + assetId.toBase58String() + "_last", auctionId),
      StringEntry("auction_" + auctionId + "_assetGen", asset),
      StringEntry("auction_" + auctionId + "_assetId", assetId.toBase58String()),
      IntegerEntry("auction_" + auctionId + "_startedAt", lastBlock.timestamp),
      StringEntry("auction_" + auctionId + "_owner", i.originCaller.toString()),
      StringEntry("auction_" + auctionId + "_description", description),
      IntegerEntry("auction_" + auctionId + "_startPrice", startPrice),
      BooleanEntry("auction_" + auctionId + "_isArtefact", assetInfo(assetId).value().issuer == Address(artefactsAddress)),
      IntegerEntry("auction_" + auctionId + "_instantPrice", instantPrice),
      StringEntry("address_" + i.originCaller.toString() + "_auction_" + auctionId + "_lockedNFT", assetId.toBase58String())
    ]
  }
}

@Callable(i)
func cancelAuction(auctionId: String) = {
  let auctionOwner = tryGetString("auction_" + auctionId + "_owner")
  if (i.originCaller.toString() != auctionOwner) then {
    throw("Only allowed to auction owner")
  } else {
    if (tryGetString("auction_" + auctionId + "_status") != "open") then {
      throw("auction is close")
    } else {
      [
        StringEntry("auction_" + auctionId + "_status", "cancelled"),
        DeleteEntry("address_" + auctionOwner + "_auction_" + auctionId + "_lockedNFT"),
        ScriptTransfer(i.originCaller, 1, this.getStringValue("auction_" + auctionId + "_assetId").fromBase58String())
      ]
    }
  }
}

@Callable(i)
func instantBuy(auctionId: String) = {
  let isEggSell = tryGetBoolean("auction_" + auctionId + "_eggSell")
  let paymentAssetId = if isEggSell == true then eggAssetId else unit
  let paymentAssetName = if isEggSell == true then "EGG" else "WAVES"

  let pmt = i.payments[0].value()
  let amount = pmt.amount
  let assetId = tryGetString("auction_" + auctionId + "_assetId")
  let bidId = i.transactionId.toBase58String()

  let auctionStatus = tryGetString("auction_" + auctionId + "_status")

  let finalFee = if tryGetBoolean("auction_" + auctionId + "_isArtefact") == true then artefactsFeePercent else feePercent

  if (pmt.assetId != paymentAssetId) then {
    throw("Auction does only support " + paymentAssetName + " payments.")
  } else {
    if (auctionStatus != "open") then {
      throw("Auction is closed. You cannot place a bid.")
    } else {
      let instantPrice = tryGetInteger("auction_" + auctionId + "_instantPrice")
      if (instantPrice > amount) then {
        throw("For instant purchase you need to pay at least " + instantPrice.toString())
      } else {
        let auctionOwner = tryGetString("auction_" + auctionId + "_owner")
        let recordPrice = tryGetInteger(assetId+"_recordPrice")
        let newRecordPrice = max([recordPrice, amount])

        [
          StringEntry("auction_" + auctionId + "_status", "finished"),
          IntegerEntry("auction_" + auctionId + "_finalPrice", amount),
          IntegerEntry(assetId + "_recordPrice", newRecordPrice),
          StringEntry("assetId_" + assetId + "_bid_" + bidId + "_data", "{\"auctionId\": \"" + auctionId + "\", \"author\": \"" + i.originCaller.toString() + "\", \"amount\": \"" + amount.toString() + "\", \"status\": \"finished\"}"),
          DeleteEntry("address_" + auctionOwner + "_auction_" + auctionId + "_lockedNFT"),
          ScriptTransfer(i.originCaller, 1, this.getStringValue("auction_" + auctionId + "_assetId").fromBase58String()),
          ScriptTransfer(addressFromStringValue(auctionOwner), fraction(amount, 100 - finalFee, 100), paymentAssetId),
          ScriptTransfer(Address(feeAggregator), fraction(amount, finalFee, 100), paymentAssetId)
        ]
      }
    }
  }
}

@Callable(i)
func placeBid(auctionId: String) = {
  let isEggSell = tryGetBoolean("auction_" + auctionId + "_eggSell")
  let paymentAssetId = if isEggSell == true then eggAssetId else unit
  let paymentAssetName = if isEggSell == true then "EGG" else "WAVES"

  let pmt = i.payments[0].value()
  let amount = pmt.amount
  let bidId = i.transactionId.toBase58String()
  let assetId = this.getStringValue("auction_" + auctionId + "_assetId")

  if (pmt.assetId != paymentAssetId) then {
    throw("Auction does only support " + paymentAssetName + " payments.")
  } else {
    let minPrice = tryGetInteger("auction_" + auctionId + "_startPrice")
    if (minPrice > amount) then {
      throw("The minimal initial price is set as " + minPrice.toString())
    } else {
      [
        StringEntry("auction_" + auctionId + "_bid_" + bidId + "_author", i.originCaller.toString()),
        StringEntry("assetId_" + assetId + "_bid_" + bidId + "_data", "{\"auctionId\": \"" + auctionId + "\", \"author\": \"" + i.originCaller.toString() + "\", \"amount\": \"" + amount.toString() + "\", \"status\": \"open\"}"),
        IntegerEntry("auction_" + auctionId + "_bid_" + bidId + "_amount", amount),
        StringEntry("address_" + i.originCaller.toString() + "_auction_" + auctionId + "_bid_" + bidId, assetId),
        StringEntry("auction_" + auctionId + "_bid_" + bidId + "_status", "open")
      ]    
    }
  }
}

@Callable(i)
func cancelBid(auctionId: String, bidId: String) = {
  let assetId = this.getStringValue("auction_" + auctionId + "_assetId")

  let bidAuthor = tryGetString("auction_" + auctionId + "_bid_" + bidId + "_author")
  let bidAmount = tryGetInteger("auction_" + auctionId + "_bid_" + bidId + "_amount")

  if (i.originCaller.toString() != bidAuthor) then {
    throw("Only allowed to the bid author")
  } else {
    if (tryGetString("auction_" + auctionId + "_bid_" + bidId + "_status") != "open") then {
      throw("bid is not open")
    } else {
      getBidCancelResults(auctionId, bidId)
    }
  }
}

@Callable(i)
func cancelOutdatedBid(auctionId: String, bidId: String) = {
  throw("to be released")
  # let auctionStatus = tryGetString("auction_" + auctionId + "_status")
  # if (tryGetString("auction_" + auctionId + "_bid_" + bidId + "_status") != "open") then {
  #   throw("bid is not open")
  # } else if (auctionStatus != "cancelled" && auctionStatus != "finished") then {
  #   throw("this oracle can cancel bids only for canceled or finished auctions")
  # } else {
  #   getBidCancelResults(auctionId, bidId)
  # }
}

@Callable(i)
func acceptBid(auctionId: String, bidId: String) = {
  let isEggSell = tryGetBoolean("auction_" + auctionId + "_eggSell")
  let paymentAssetId = if isEggSell == true then eggAssetId else unit

  let auctionOwner = tryGetString("auction_" + auctionId + "_owner")
  let bidAuthor = tryGetString("auction_" + auctionId + "_bid_" + bidId + "_author")
  let bidAmount = tryGetInteger("auction_" + auctionId + "_bid_" + bidId + "_amount")
  let bidStatus = tryGetString("auction_" + auctionId + "_bid_" + bidId + "_status")
  let assetId = tryGetString("auction_" + auctionId + "_assetId")


  let finalFee = if tryGetBoolean("auction_" + auctionId + "_isArtefact") == true then artefactsFeePercent else feePercent

  if (i.originCaller.toString() != auctionOwner) then {
    throw("Only allowed to auction owner")
  } else {
    if (bidStatus != "open") then {
      throw("Bid was cancelled and cannot be accepted")
    } else if (tryGetString("auction_" + auctionId + "_status") != "open") then {
      throw("Auction was closed")
    } else {
      let recordPrice = tryGetInteger(assetId+"_recordPrice")
      let newRecordPrice = max([recordPrice, bidAmount])
      [
        StringEntry("auction_" + auctionId + "_bid_" + bidId + "_status", "finished"),
        StringEntry("auction_" + auctionId + "_status", "finished"),
        StringEntry("assetId_" + assetId + "_bid_" + bidId + "_data", "{\"auctionId\": \"" + auctionId + "\", \"author\": \"" + i.originCaller.toString() + "\", \"amount\": \"" + bidAmount.toString() + "\", \"status\": \"finished\"}"),
        IntegerEntry("auction_" + auctionId + "_finalPrice", bidAmount),
        IntegerEntry(assetId + "_recordPrice", newRecordPrice),
        DeleteEntry("address_" + bidAuthor + "_auction_" + auctionId + "_bid_" + bidId),
        DeleteEntry("address_" + auctionOwner + "_auction_" + auctionId + "_lockedNFT"),
        ScriptTransfer(i.originCaller, fraction(bidAmount, 100 - finalFee, 100), paymentAssetId),
        ScriptTransfer(Address(feeAggregator), fraction(bidAmount, finalFee, 100), paymentAssetId),
        ScriptTransfer(addressFromStringValue(bidAuthor), 1, assetId.fromBase58String())
      ]
    }
  }
}


@Verifier(tx)
func verify() = sigVerify(tx.bodyBytes, tx.proofs[0], tx.senderPublicKey)
