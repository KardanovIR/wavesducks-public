{-# STDLIB_VERSION 5 #-}
{-# SCRIPT_TYPE ACCOUNT #-}
{-# CONTENT_TYPE DAPP #-}

let mAccPKey = base58'HyXCiJpt1eh1M1zZWDpYzbVW81yZL4XBjiJjGwxZqk2'
let mAccAddr = mAccPKey.addressFromPublicKey()
let feePercent = getIntegerValue(mAccAddr, "f_" + this.toString() + "_fee")
let stakingAddress = valueOrErrorMessage(getString(mAccAddr, "f_" + this.toString() + "_stake_address"),"f_" + this.toString() + "_stake_address not defined").addressFromStringValue()

let kTotalLiquidity = "total_liquidity"
let kInitialized = "INITIALIZED"
let kLiquidityThreshold = "LIQUIDITY_THRESHOLD"
let kFarmParticipants = "farm_participants"
let kTotalShareTokens = "total_share_tokens"
let kShareAssetId = "SHARE_ASSET_ID"
let kIncubatorDapp = "INCUBATOR_DAPP"
let kBreederDapp = "BREEDER_DAPP"
let kFarmingDapp = "FARMING_DAPP"
let kMarketProxyDapp = "MARKETPLACE_PROXY_DAPP"
let kAuctionDapp = "AUCTION_DAPP"
let kLocked = "LOCKED"
let kCeoCollectedFee = "ceo_collected_fee"
let kCeoCollectedFeeOverall = "ceo_fee_overall"
let kLockedInvestments = "locked_investments"

let keyTotalCompound = "total_compound"
let keyGlobalStaked = "global_staked"

func getInt(key: String) = {
  match getInteger(this, key) {
      case b:Int => b
     case _ => 0
  }
}

func getStr(key: String) = {
  match getString(this, key) {
      case b:String => b
      case _ => ""
  }
}

func getBool(key: String) = {
  match getBoolean(this, key) {
      case b:Boolean => b
      case _ => unit
  }
}

func getStrA(a: Address, k: String) = {
  match getString(a, k) {
      case b:String => b
      case _ => ""
  }
}

func getIntA(address: Address, key: String) = {
    match getInteger(address, key) {
        case b:Int => b
        case _ => 0
    }
}

func getMarketProxyAddr() = {
  getStrA(mAccAddr, kMarketProxyDapp)
}



func getEggId() = {
  getStringValue(mAccAddr, "EGG_ASSET_ID").fromBase58String()
}
func getShareAssetId() = {
  getStr(kShareAssetId).fromBase58String()
}

@Callable(i)
func calculateCompoundShareAndIssue(amount: Int) = {
  if (i.caller != this) then {
    throw("You cannot do this action")
  } else {
    if (getBool(kLockedInvestments) == true) 
    then  ([],
          0)
    else{
      let shareAssetId = getShareAssetId()
      let totalCompound = getIntA(stakingAddress,keyTotalCompound)
      let totalStaked   = getIntA(stakingAddress,keyGlobalStaked)
      let totalLocked = totalStaked+totalCompound
      let amountForCompound = fraction(amount,totalCompound,totalLocked)
      let totalShareTokens = getInt(kTotalShareTokens)

      (
        [
          Reissue(shareAssetId, amountForCompound, true),
          IntegerEntry(kTotalShareTokens, totalShareTokens + amountForCompound)
        ],
        amountForCompound
      )
    }
  }


}

@Callable(i)
func initCollectiveFarm(name: String, minimumThreshold: Int) = {
  if (i.callerPublicKey != mAccPKey) then {
      throw("_1")
  }else if (getBool(kInitialized) != unit) then {
      throw("_2")
  } else if (i.payments[0].amount != 1000_0000 || i.payments[0].assetId != getEggId()) then {
      throw("_20")
  } else {
      let shareTokens = 1000_0000
      let shareToken = Issue(name, "Team token for the \"" + name + "\" collective farm", shareTokens, 8, true)
      
      [
        StringEntry(kShareAssetId, shareToken.calculateAssetId().toBase58String()),
        BooleanEntry(kInitialized, true),
        IntegerEntry(kTotalLiquidity, 10),
        IntegerEntry(kTotalShareTokens, shareTokens),
        shareToken,
        ScriptTransfer(i.caller, shareTokens, shareToken.calculateAssetId()),
        BooleanEntry(kLocked, false),
        IntegerEntry(kLiquidityThreshold, minimumThreshold)
      ]
  }
}

@Callable(i)
func setLock(s: Boolean) = {
  if (i.callerPublicKey != mAccPKey) then {
    throw("_21")
  } else {
      [
        BooleanEntry(kLocked, s)
      ]
  }
}


@Callable(i)
func lockInvestments() = {
  if (i.caller != this) then {
    throw("You cannot lock this contract")
  } else {
      [
        BooleanEntry(kLockedInvestments, true)
      ]
  }
}

@Callable(i)
func unlockInvestments() = {
  if (i.caller != this) then {
    throw("You cannot lock this contract")
  } else {
      [
        BooleanEntry(kLockedInvestments, false)
      ]
  }
}

@Callable(i)
func provideLiquidity() = {
  let eggAssetId = getEggId()
  if (getBool(kLockedInvestments) == true) then throw("Contract is locked for investments by it's CEO. Please invest in another collective farm.") else
  if (getBool(kLocked) == true) then throw("_22") else
  if (i.payments[0].assetId != eggAssetId) then throw("_4") else
  if (i.payments[0].amount < 100_0000) then throw("Min amount to invest is 0.01 EGG")
  else {
    let totalLiquidity = getInt(kTotalLiquidity)
    let totalShareTokens = getInt(kTotalShareTokens)
    let shareTokensToGive = i.payments[0].amount # decimals difference

    let shareTokensId = getStr(kShareAssetId).fromBase58String()

    let reissue = Reissue(getShareAssetId(), shareTokensToGive, true)
    let farmParticipants = getInt(kFarmParticipants)
    let kParticipated = "account_" + i.caller.toString() + "_participated"
    let alreadyParticipated = getBool(kParticipated)
    let newFarmParticipants = if (alreadyParticipated == unit) then farmParticipants + 1 else farmParticipants
    ([
      reissue,
      ScriptTransfer(i.caller, shareTokensToGive, shareTokensId),
      IntegerEntry(kTotalShareTokens, totalShareTokens + shareTokensToGive),
      IntegerEntry(kTotalLiquidity, totalLiquidity + (i.payments[0].amount.value() / 100_0000)),
      IntegerEntry(kFarmParticipants, newFarmParticipants),
      BooleanEntry(kParticipated, true)
    ],
    shareTokensToGive
    )
  }
}

@Callable(i)
func claimFarmingRewardProxy(ids: String) = {
  if (i.callerPublicKey != mAccPKey) then throw("_23")
  else {
    let eggAssetId = getEggId()
    let shareAssetId = getShareAssetId()

    let farmingDapp = getStrA(mAccAddr, kFarmingDapp).addressFromStringValue()

    func claimReward(accum: Int, nftId: String) = {
      strict invRes = invoke(farmingDapp, "claimReward", [nftId], [])
      match invRes {
        case int: Int => accum + int
        case _ => throw("_5")
      }
    }

    let nftIds = ids.split(";")
    let foldResult = FOLD<10>(nftIds, 0, claimReward)
    let claimedReward = match foldResult {
      case int: Int => int
      case _ => throw("_6")
    }
    let farmCeoFeeSize = fraction(claimedReward, feePercent, 100)
    let currentCeoFee = getInt(kCeoCollectedFee)
    let ceoFeeOverall = getInt(kCeoCollectedFeeOverall)
    let amountToPay = claimedReward-farmCeoFeeSize
    
    strict amountCompontCall = invoke(this,"calculateCompoundShareAndIssue",[amountToPay],[])
    let compound = match amountCompontCall {
      case int: Int => int
      case _ => throw("Error during compound calcuations")
    }
    strict topUp = invoke(stakingAddress,"topUpReward",[],[
                          AttachedPayment(eggAssetId,amountToPay-compound),
                          AttachedPayment(shareAssetId,compound)]
                          )
    [
      IntegerEntry(kCeoCollectedFee, currentCeoFee + farmCeoFeeSize),
      IntegerEntry(kCeoCollectedFeeOverall, ceoFeeOverall + farmCeoFeeSize)
    ]
  }
}

@Callable(i)
func claimCeoFee(address1: String, address2: String, address1Share: Int, address2Share: Int) = {
  if (address1Share + address2Share != 100) then throw("Sum of shares should be 100") else
  if (getBool(kLocked) == true) then throw("_22") else
  if (i.caller == this) then {
    let currentCeoCollectedFee = getInt(kCeoCollectedFee)
    let address1Reward = if (address2 == "") then currentCeoCollectedFee else fraction(currentCeoCollectedFee, address1Share, 100)
    let address2Reward = if (address2 == "") then 0 else fraction(currentCeoCollectedFee, address2Share, 100)
    [
      ScriptTransfer(address1.addressFromStringValue(), address1Reward, getEggId()),
      ScriptTransfer(address2.addressFromStringValue(), address2Reward, getEggId()),
      IntegerEntry(kCeoCollectedFee, 0)
    ]
  } else throw("Only manager can call the contract")
}

@Callable(i)
func startLiquidation(auctionId: String) = {
  throw("_7")
}

@Callable(i)
func voteForLiquidation(auctionId: String) = {
  throw("_7")
}

@Callable(i)
func callUnstakeProxy(function: String, assetId: String) = {
  if (getBool(kLocked) == true) then throw("Contract is locked now") else
  if (function != "unstakeNFT" && function != "unstakeJackpot") then throw("Bad function name") else
  if (i.caller != this) then throw("Can be called only by the account") else {
    let eggAssetId = getEggId()
    let shareAssetId = getShareAssetId()
    let farmingDappAddress = getStrA(mAccAddr, "FARMING_DAPP").addressFromStringValue()
    strict unstakedReward = invoke(farmingDappAddress, function, [assetId], [])

    let reward = match unstakedReward {
      case int: Int =>  int
      case _ => throw("Error during unstaking")
    }


    let currentCeoFee = getInt(kCeoCollectedFee)
    let ceoFeeOverall = getInt(kCeoCollectedFeeOverall)
    let farmCeoFeeSize = fraction(reward, feePercent, 100)

    let amountToPay = reward-farmCeoFeeSize
    strict amountCompontCall = invoke(this,"calculateCompoundShareAndIssue",[amountToPay],[])
    let compound = match amountCompontCall {
      case int: Int => int
      case _ => throw("Error during compound calcuations")
    }
    strict topUp = invoke(stakingAddress,"topUpReward",[],[
                          AttachedPayment(eggAssetId,amountToPay-compound),
                          AttachedPayment(shareAssetId,compound)]
                          )
    [
      IntegerEntry(kCeoCollectedFee, currentCeoFee + farmCeoFeeSize),
      IntegerEntry(kCeoCollectedFeeOverall, ceoFeeOverall + farmCeoFeeSize)
    ]
  }
}

@Callable(i)
func revertTransfer(txId: String) = {
  if (getBool(kLocked) == true) then throw("Contract is locked now") else
  if (i.caller != this) then throw("Can be called only by the account") else {
    let tx = transferTransactionById(txId.fromBase58String()).value()
    let kReverted = "tx_revert_" + txId
    let txReverted = getBool(kReverted)
    let currentEggBalance = assetBalance(this, getEggId())
    let result = [
      ScriptTransfer(tx.sender, tx.amount, tx.assetId),
      BooleanEntry(kReverted, true)
    ]

    if (txReverted == true) then throw("Tx already reverted") else 
    if (tx.recipient != this) then throw("Tx recipient is another account") else
    if (tx.assetId == getShareAssetId()) then result else
    if (tx.assetId != getEggId()) then throw("Transaction is not with EGG or share tokens") else 
    if ((currentEggBalance - tx.amount) < 0) then throw("Revert makes balance too low") else {
      result
    }
  }
}

@Callable(i)
func claimReward() = {
  let addresses = getStr("rew1")
  let addressParts = addresses.split(";")

  func findReward (acc: Int, val: String) = {
    let parts = val.split("=").value()
    if (parts[0].value() == i.caller.toString()) then acc + parseIntValue(parts[1]) else 0
  }

  let reward = FOLD<83>(addressParts, 0, findReward)
  [
    IntegerEntry("a", reward)
  ]
}

@Verifier(tx)
func verify() = {
  let checkStarted = getInt(kTotalLiquidity) >= getInt(kLiquidityThreshold)

  func totalEggSpending(accum: Int, payment: AttachedPayment) = {
    if (payment.assetId == getEggId()) then accum + payment.amount else accum
  }

  match (tx) {
      case inv:InvokeScriptTransaction => {
        let acceptedCallsStaking = ["stakeFarmTokens","claimReward","withdrawFarmTokens"]
        let acceptedCallsThis = ["callUnstakeProxy","revertTransfer","claimCeoFee","lockInvestments","unlockInvestments"]
        # Avoid scam with high fees => allow only Waves and minimal amount


        if (inv.function == "unstakeNFT" || inv.function == "unstakeJackpot") then throw("You cannot call these functions directly") else
        if (sigVerify(tx.bodyBytes, tx.proofs[0], tx.senderPublicKey) == false) then throw("_100") else
        if (checkStarted == false) then throw("_14") else
        if (getBool(kLocked) == true) then throw("_22") else
        if (inv.fee > 900000 || inv.feeAssetId != unit) then {
          throw("_15")
        }else if (inv.dApp.addressFromRecipient() != stakingAddress && inv.function == "claimReward") then {
          throw("_16")
        } else if (inv.payments.size() > 2) then {
          throw("_17")
        } else if (inv.dApp.addressFromRecipient() == this && containsElement(acceptedCallsThis,inv.function)) then {
          sigVerify(tx.bodyBytes, tx.proofs[0], tx.senderPublicKey)
        } else if (inv.dApp.addressFromRecipient() == getMarketProxyAddr().addressFromStringValue() && inv.function == "callMarketplaceProxy") then {
          sigVerify(tx.bodyBytes, tx.proofs[0], tx.senderPublicKey)
        } else if (inv.dApp.addressFromRecipient() == stakingAddress && containsElement(acceptedCallsStaking,inv.function) ) then {
          sigVerify(tx.bodyBytes, tx.proofs[0], tx.senderPublicKey)
        }else {
          let allowedContracts = getStrA(mAccAddr, "allowed_contracts")
          let allowedContractsList = allowedContracts.split(";")
          let dappToCall = inv.dApp.addressFromRecipient().toString()
          allowedContractsList.indexOf(dappToCall) != unit
        }
      }
      case ss: SetScriptTransaction => {
        sigVerify(tx.bodyBytes, tx.proofs[0], mAccPKey)
      }
      case mt: MassTransferTransaction => {
        sigVerify(tx.bodyBytes, tx.proofs[0], mAccPKey)
      }
      case dt: DataTransaction => {
        sigVerify(tx.bodyBytes, tx.proofs[0], mAccPKey)
      }
      case _ => false
    }
}
