{-# STDLIB_VERSION 5 #-}
{-# SCRIPT_TYPE ACCOUNT #-}
{-# CONTENT_TYPE DAPP #-}

let mAccPKey = base58'23NWm7kUDQqbTVkirjd5JXWRZaWVoS4ZesmRNHicBa3T'
let mAccAddr = mAccPKey.addressFromPublicKey()
let feePercent = getIntegerValue(mAccAddr, "f_" + this.toString() + "_fee")
let oldEggAssetId = base58'JCGDtrVy64cCJ1wCKfCaiNQMnyYwii71TbE5QeAHfxgF'
let swapEggDappAddress = "3PJQUUiJdvz9etUKED9ju7o7VrcNMtnkXBU".addressFromStringValue()

let kTotalLiquidity = "total_liquidity"
let kInitialized = "INITIALIZED"
let kLiquidityThreshold = "LIQUIDITY_THRESHOLD"
let kFarmParticipants = "farm_participants"
let kTotalShareTokens = "total_share_tokens"
let kShareAssetId = "SHARE_ASSET_ID"
let kIncubatorDapp = "INCUBATOR_DAPP"
let kBreederDapp = "BREEDER_DAPP"
let kFarmingDapp = "FARMING_DAPP"
let kMarketProxyDapp = "MARKETPLACE_PROXY_DAPP"
let kAuctionDapp = "AUCTION_DAPP"
let kLocked = "LOCKED"
let kTotalFarmingReward = "total_farming_reward"
let kCeoCollectedFee = "ceo_collected_fee"
let kCeoCollectedFeeOverall = "ceo_fee_overall"
let kToDistr = "to_distribute"
let kUnstakedReward = "unstaked_reward"
let kLockedInvestments = "locked_investments"

func getInt(key: String) = {
  match getInteger(this, key) {
      case b:Int => b
     case _ => 0
  }
}

func getStr(key: String) = {
  match getString(this, key) {
      case b:String => b
      case _ => ""
  }
}

func getBool(key: String) = {
  match getBoolean(this, key) {
      case b:Boolean => b
      case _ => unit
  }
}

func getStrA(a: Address, k: String) = {
  match getString(a, k) {
      case b:String => b
      case _ => ""
  }
}

func getIntA(address: Address, key: String) = {
    match getInteger(address, key) {
        case b:Int => b
        case _ => 0
    }
}

func getMarketProxyAddr() = {
  getStrA(mAccAddr, kMarketProxyDapp)
}

func keyAutocompound(address: Address) = {
  "address_" + address.toString() + "_autocompound"
}

func getEggId() = {
  getStringValue(mAccAddr, "EGG_ASSET_ID").fromBase58String()
}
func getShareAssetId() = {
  getStr(kShareAssetId).fromBase58String()
}

@Callable(i)
func initCollectiveFarm(name: String, minimumThreshold: Int) = {
  if (i.callerPublicKey != mAccPKey) then {
      throw("_1")
  }else if (getBool(kInitialized) != unit) then {
      throw("_2")
  } else if (i.payments[0].amount != 10 * 1_000_000 || i.payments[0].assetId != getEggId()) then {
      throw("_20")
  } else {
      let shareTokens = 1_0_000_000
      let shareToken = Issue(name, "Share token for the \"" + name + "\" collective farm", shareTokens, 8, true)
      
      [
        StringEntry(kShareAssetId, shareToken.calculateAssetId().toBase58String()),
        BooleanEntry(kInitialized, true),
        IntegerEntry(kTotalLiquidity, 10),
        IntegerEntry(kTotalShareTokens, shareTokens),
        shareToken,
        ScriptTransfer(i.caller, shareTokens, shareToken.calculateAssetId()),
        BooleanEntry(kLocked, false),
        IntegerEntry(kLiquidityThreshold, minimumThreshold)
      ]
  }
}

@Callable(i)
func setLock(s: Boolean) = {
  if (i.callerPublicKey != mAccPKey) then {
    throw("_21")
  } else {
      [
        BooleanEntry(kLocked, s)
      ]
  }
}


@Callable(i)
func lockInvestments() = {
  if (i.caller != this) then {
    throw("You cannot lock this contract")
  } else {
      [
        BooleanEntry(kLockedInvestments, true)
      ]
  }
}

@Callable(i)
func unlockInvestments() = {
  if (i.caller != this) then {
    throw("You cannot lock this contract")
  } else {
      [
        BooleanEntry(kLockedInvestments, false)
      ]
  }
}

@Callable(i)
func provideLiquidity() = {
  let eggAssetId = getEggId()
  if (getBool(kLockedInvestments) == true) then throw("Contract is locked for investments by it's CEO. Please invest in another collective farm.") else
  if (getBool(kLocked) == true) then throw("_22") else
  if (i.payments[0].assetId != eggAssetId) then throw("_4") else
  if (i.payments[0].amount < 1_000_000) then throw("Min amount to invest is 0.01 EGG")
  else {
    let totalLiquidity = getInt(kTotalLiquidity)
    let totalShareTokens = getInt(kTotalShareTokens)
    let shareTokensToGive = i.payments[0].amount # decimals difference

    let shareTokensId = getStr(kShareAssetId).fromBase58String()

    let reissue = Reissue(getShareAssetId(), shareTokensToGive, true)
    let farmParticipants = getInt(kFarmParticipants)
    let kParticipated = "account_" + i.caller.toString() + "_participated"
    let alreadyParticipated = getBool(kParticipated)
    let newFarmParticipants = if (alreadyParticipated == unit) then farmParticipants + 1 else farmParticipants

    [
      reissue,
      ScriptTransfer(i.caller, shareTokensToGive, shareTokensId),
      IntegerEntry(kTotalShareTokens, totalShareTokens + shareTokensToGive),
      IntegerEntry(kTotalLiquidity, totalLiquidity + (i.payments[0].amount.value() / 1_000_000)),
      IntegerEntry(kFarmParticipants, newFarmParticipants),
      BooleanEntry(kParticipated, true)
    ]
  }
}

# @Callable(i)
# func clearFarmingState() = {
#   if (i.caller != mAccAddr) then throw("Can be called only by master") else 
#   [
#     DeleteEntry(kTotalFarmingReward),
#     DeleteEntry(kToDistr),
#     DeleteEntry(kCeoCollectedFee),
#     DeleteEntry(kCeoCollectedFeeOverall),
#     DeleteEntry(kUnstakedReward)
#   ]
# }

@Callable(i)
func claimFarmingRewardProxy(ids: String) = {
  if (i.callerPublicKey != mAccPKey) then throw("_23")
  else {
    let farmingDapp = getStrA(mAccAddr, kFarmingDapp).addressFromStringValue()

    func claimReward(accum: Int, nftId: String) = {
      strict invRes = invoke(farmingDapp, "claimReward", [nftId], [])
      match invRes {
        case int: Int => accum + int
        case _ => throw("_5")
      }
    }

    let nftIds = ids.split(";")
    let foldResult = FOLD<10>(nftIds, 0, claimReward)
    let claimedReward = match foldResult {
      case int: Int => int
      case _ => throw("_6")
    }
    let farmCeoFeeSize = fraction(claimedReward, feePercent, 100)

    let toDistr = getInt(kToDistr)
    let totalFarmingReward = getInt(kTotalFarmingReward)
    let currentCeoFee = getInt(kCeoCollectedFee)
    let ceoFeeOverall = getInt(kCeoCollectedFeeOverall)

    [
      IntegerEntry(kTotalFarmingReward, totalFarmingReward + claimedReward - farmCeoFeeSize),
      IntegerEntry(kToDistr, toDistr + claimedReward - farmCeoFeeSize),
      IntegerEntry(kCeoCollectedFee, currentCeoFee + farmCeoFeeSize),
      IntegerEntry(kCeoCollectedFeeOverall, ceoFeeOverall + farmCeoFeeSize)
    ]
  }
}

@Callable(i)
func claimCeoFee(address: String) = {
  if (getBool(kLocked) == true) then throw("_22") else
  if (i.caller == this) then {
    let currentCeoCollectedFee = getInt(kCeoCollectedFee)
    [
      ScriptTransfer(address.addressFromStringValue(), currentCeoCollectedFee, getEggId()),
      IntegerEntry(kCeoCollectedFee, 0)
    ]
  } else throw("_8")
}

@Callable(i)
func startLiquidation(auctionId: String) = {
  throw("_7")
}

@Callable(i)
func voteForLiquidation(auctionId: String) = {
  throw("_7")
}

@Callable(i)
func callUnstakeProxy(function: String, assetId: String) = {
  if (getBool(kLocked) == true) then throw("Contract is locked now") else
  if (function != "unstakeNFT" && function != "unstakeJackpot") then throw("Bad function name") else
  if (i.caller != this) then throw("Can be called only by the account") else {
    let farmingDappAddress = getStrA(mAccAddr, "FARMING_DAPP").addressFromStringValue()
    strict ustakedReward = invoke(farmingDappAddress, function, [assetId], [])
    let unstakedRewardNow = getInt(kUnstakedReward)

    let newUnstakedReward = match ustakedReward {
      case int: Int => unstakedRewardNow + (int)
      case _ => throw("Error during unstaking")
    }

    let currentCeoFee = getInt(kCeoCollectedFee)
    let ceoFeeOverall = getInt(kCeoCollectedFeeOverall)
    let farmCeoFeeSize = fraction(unstakedRewardNow, feePercent, 100)

    [
      IntegerEntry(kCeoCollectedFee, currentCeoFee + farmCeoFeeSize),
      IntegerEntry(kCeoCollectedFeeOverall, ceoFeeOverall + farmCeoFeeSize),
      IntegerEntry(kUnstakedReward, newUnstakedReward)
    ]
  }
}

@Callable(i)
func revertTransfer(txId: String) = {
  if (getBool(kLocked) == true) then throw("Contract is locked now") else
  if (i.caller != this) then throw("Can be called only by the account") else {
    let tx = transferTransactionById(txId.fromBase58String()).value()
    let kReverted = "tx_revert_" + txId
    let txReverted = getBool(kReverted)

    let unstakedRewardNow = getInt(kUnstakedReward)
    let toDistr = getInt(kToDistr)

    let minBalance = unstakedRewardNow + toDistr
    let currentEggBalance = assetBalance(this, getEggId())
    let result = [
      ScriptTransfer(tx.sender, tx.amount, tx.assetId),
      BooleanEntry(kReverted, true)
    ]

    if (txReverted == true) then throw("Tx already reverted") else 
    if (tx.recipient != this) then throw("Tx recipient is another account") else
    if (tx.assetId == getShareAssetId()) then result else
    if (tx.assetId != getEggId()) then throw("Transaction is not with EGG or share tokens") else 
    if ((currentEggBalance - tx.amount) < minBalance) then throw("Revert makes balance too low") else {
      result
    }
  }
}

@Callable(i)
func setAutocompound(val: Boolean) = {
  let kAutocompound = keyAutocompound(i.originCaller)
  [
    BooleanEntry(kAutocompound, val)
  ]
}

#address1=number-of-share-tokens;address2=number-of-share-tokens;...
@Callable(i)
func distributeRewards(addresses: String, totalShareTokens: Int, totalRewardToDistribute: Int) = {
  if (i.caller != mAccAddr) then throw("Method can be called only by master account") else
  let addressPairs = addresses.split(";")

  func processAddress(accum: (Int, List[IntegerEntry | ScriptTransfer]), addressPair: String) = {
    let addressParts = addressPair.split("=")
    let addressStr = addressParts[0]
    let address = addressStr.addressFromStringValue()
    let userShareTokens = addressParts[1].parseIntValue()

    let kAddrEarned = "address_" + addressStr + "_total_earned"
    let addrTotalEarned = getInt(kAddrEarned)
    let userRewardNow = fraction(totalRewardToDistribute, userShareTokens, totalShareTokens)
    let autocompoundEnabled = getBool(keyAutocompound(addressStr.addressFromStringValue())) == true && getBool(kLockedInvestments) != true 
    let assetToSend = if autocompoundEnabled == true then getShareAssetId() else getEggId()

    let res = ([
      IntegerEntry(kAddrEarned, addrTotalEarned + userRewardNow),
      IntegerEntry("address_" + addressStr + "_reward_" + lastBlock.timestamp.toString(), userRewardNow)
    ] 
    ++ if autocompoundEnabled == true then [Reissue(assetToSend, userRewardNow, true)] else []
    ++ if address != this then [ScriptTransfer(address, userRewardNow, assetToSend)] else [])
    (accum._1 + userRewardNow, accum._2 ++ res)
  }

  let result = FOLD<20>(addressPairs, (0, []), processAddress)
  let toDistr = getInt(kToDistr)
  result._2 ++ [IntegerEntry(kToDistr, toDistr - result._1), IntegerEntry(kUnstakedReward, 0)]
}

@Verifier(tx)
func verify() = {
  let checkStarted = getInt(kTotalLiquidity) >= getInt(kLiquidityThreshold)

  func totalEggSpending(accum: Int, payment: AttachedPayment) = {
    if (payment.assetId == getEggId()) then accum + payment.amount else accum
  }

  match (tx) {
      case inv:InvokeScriptTransaction => {
        let unstakedRewardNow = getInt(kUnstakedReward)
        let toDistr = getInt(kToDistr)

        let minBalance = unstakedRewardNow + toDistr

        # Avoid scam with high fees => allow only Waves and minimal amount


        if (inv.function == "unstakeNFT" || inv.function == "unstakeJackpot") then throw("You cannot call these functions directly") else
        if (sigVerify(tx.bodyBytes, tx.proofs[0], tx.senderPublicKey) == false) then throw("_100") else
        if (checkStarted == false) then throw("_14") else
        if (getBool(kLocked) == true) then throw("_22") else
        if (inv.fee > 900000 || inv.feeAssetId != unit) then {
          throw("_15")
        }else if (inv.function == "claimReward") then {
          throw("_16")
        } else if (inv.payments.size() > 2) then {
          throw("_17")
        } else if ((assetBalance(this, getEggId()) - FOLD<3>(inv.payments, 0, totalEggSpending)) * 1_000_000 < minBalance) then {
          throw("Your minimal balance should be = " + minBalance.toString())
        } else if (inv.dApp.addressFromRecipient() == this && inv.function == "callUnstakeProxy") then {
          sigVerify(tx.bodyBytes, tx.proofs[0], tx.senderPublicKey)
        } else if (inv.dApp.addressFromRecipient() == this && inv.function == "revertTransfer") then {
          sigVerify(tx.bodyBytes, tx.proofs[0], tx.senderPublicKey)
        } else if (inv.dApp.addressFromRecipient() == this && inv.function == "claimCeoFee") then {
          sigVerify(tx.bodyBytes, tx.proofs[0], tx.senderPublicKey)
        } else if (inv.dApp.addressFromRecipient() == this && (inv.function == "lockInvestments" || inv.function == "unlockInvestments" )) then {
          sigVerify(tx.bodyBytes, tx.proofs[0], tx.senderPublicKey)
        } else if (inv.dApp.addressFromRecipient() == getMarketProxyAddr().addressFromStringValue() && inv.function == "callMarketplaceProxy") then {
          sigVerify(tx.bodyBytes, tx.proofs[0], tx.senderPublicKey)
        } else {
          let allowedContracts = getStrA(mAccAddr, "allowed_contracts")
          let allowedContractsList = allowedContracts.split(";")
          let dappToCall = inv.dApp.addressFromRecipient().toString()
          allowedContractsList.indexOf(dappToCall) != unit
        }
      }
      case ss: SetScriptTransaction => {
        sigVerify(tx.bodyBytes, tx.proofs[0], mAccPKey)
      }
      case mt: MassTransferTransaction => {
        sigVerify(tx.bodyBytes, tx.proofs[0], mAccPKey)
      }
      case dt: DataTransaction => {
        sigVerify(tx.bodyBytes, tx.proofs[0], mAccPKey)
      }
      case _ => false
    }
}
